import groovy.json.JsonSlurper
import groovy.xml.MarkupBuilder

def convertUnitTestsReport() {
    def json = readFile file: "${env.report_output_path}\\index.json", encoding: "UTF-8"
    // Needed because the JSON is encoded in UTF-8 with BOM
    json = json.replace( "\uFEFF", "" );

    def xml_content = getJUnitXMLContentFromJSON( json )

    writeFile file: "${env.report_output_path}\\junit.xml", text: xml_content.toString()

    junit "Automation\\TestReportOutput\\junit.xml"
}

@NonCPS
def getJUnitXMLContentFromJSON( String json_content ) {
    def j = new JsonSlurper().parseText( json_content )
    
    def sw = new StringWriter()
    def builder = new MarkupBuilder( sw )

    builder.doubleQuotes = true
    builder.mkp.xmlDeclaration version: "1.0", encoding: "utf-8"

    builder.testsuite( tests: j.succeeded + j.failed, failures: j.failed, time: j.totalDuration ) {
        for ( test in j.tests ) {
            builder.testcase( name: test.testDisplayName, classname: test.fullTestPath, status: test.state ) {
                for ( entry in test.entries ) {
                    if (entry.event.type != "Info") {
                      builder.failure( message: entry.event.message, type: entry.event.type, entry.filename + " " + entry.lineNumber )
                    }
                }
            }
        }
    } 

    return sw.toString()
}

pipeline {
  agent {
    label 'wcn'
  }

  parameters {
    string(name: 'JENKINS_WS', defaultValue: 'C:\\Users\\zanni\\Desktop\\Jenkins\\workspace\\Potter', description: 'Location of the Jenkins workspace folder of this project')
    string(name: 'JENKINS_BUILD', defaultValue: 'C:\\Users\\zanni\\Desktop\\Jenkins\\workspace\\Builds\\Potter', description: 'Location of the Jenkins build folder of this project')
    string(name: 'JENKINS_ARCHIVE', defaultValue: 'C:\\Users\\zanni\\Desktop\\Jenkins\\workspace\\Archive\\Potter', description: 'Location of the Jenkins archive folder of this project')
    string(name: 'UE_LOC', defaultValue: 'C:\\Program Files\\Epic Games\\UE_4.22', description: 'Location of the Unreal Engine installation')
  }

  stages {
    stage('Start build') {
      steps {
        bat 'Jenkins\\Build\\01_StartBuild.bat'
      }
    }
    stage('Compile scripts') {
      steps {
        bat 'Jenkins\\Build\\02_CompileScripts.bat'
      }
    }
    stage('Build files') {
      steps {
        bat 'Jenkins\\Build\\03_BuildFiles.bat'
      }
    }
    stage('Cook project') {
      steps {
        bat 'Jenkins\\Build\\04_CookProject.bat'
      }
    }
    stage('Run tests') {
      steps {
        script {
          env.report_output_path = "${params.JENKINS_WS}\\Automation\\TestReportOutput"
        }
        bat 'Jenkins\\Test\\RunUnitTests.bat'
        convertUnitTestsReport()
        bat 'Jenkins\\Test\\RunFunctionalTests.bat'
        convertUnitTestsReport()
      }
    }
  }

  post {
    success {
      bat 'Jenkins\\Build\\05_Archive.bat'
    }

    cleanup {
      cleanWs()
    }
  }
}
